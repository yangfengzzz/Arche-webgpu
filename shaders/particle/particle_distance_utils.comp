// -----------------------------------------------------------------------------
//
//      Distance functions helpers for simple shapes and CSG operations.
//
//      ref : 'modeling with distance functions' - Inigo Quilez
//
//             This is not a MAIN shader, it must be included.
//
//------------------------------------------------------------------------------

#ifndef SHADER_DISTANCE_UTILS_GLSL_
#define SHADER_DISTANCE_UTILS_GLSL_

//MARK: - CSG operations

float opUnion(float d1, float d2) {
    return min(d1, d2);
}

float opSmoothUnion(float d1, float d2, float k) {
    float r = exp(-k*d1) + exp(-k*d2);
    return -log(r) / k;
}

float opIntersection(float d1, float d2) {
    return max(d1, d2);
}

float opSubstraction(float d1, float d2) {
    return max(d1, -d2);
}

vec3 opRepeat(vec3 p, vec3 c) {
    return mod(p, c) - 0.5f*c;
}

float opDisplacement(vec3 p, float d) {
    p = d*p;
    return sin(p.x)*sin(p.y)*sin(p.z);
}

//MARK: - PRIMITIVE ----------------------------------------------------------------------------------------------------

float sdPlane(vec3 p, vec4 n) {
    //n.xyz = normalize(n.xyz);
    return n.w + dot(p, n.xyz);
}

float sdSphere(vec3 p, float r) {
    return length(p) - r;
}

float udRoundBox(vec3 p, vec3 b, float r) {
    return length(max(abs(p)-b, 0.0f)) - r;
}

float sdCylinder(vec3 p, float c) {
    return length(p.xy) - c;
}

float sdCylinder(vec3 p, vec3 c) {
    return opIntersection(length(p.xz-c.xy) - c.z, abs(p.y)-c.y);
}

float sdTorus(vec3 p, vec2 t) {
    vec2 q = vec2(length(p.xz) - t.x, p.y);
    return length(q) - t.y;
}

#endif  // SHADER_DISTANCE_UTILS_GLSL_
